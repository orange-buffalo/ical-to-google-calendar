buildscript {
    ext {
        ical4jVersion = "3.0.6"
        commonsIoVersion = "2.6"
        lombokVersion = "1.18.4"
        jsr305Version = "3.0.2"
        junitVersion = "4.12"
        googleApiVersion = "1.28.0"
        googleCanedarApiVersion = "v3-rev364-1.25.0"
    }
}

plugins {
    id 'org.springframework.boot' version '2.1.2.RELEASE'
    id 'com.bmuschko.docker-remote-api' version '4.4.0'
}

import com.bmuschko.gradle.docker.tasks.image.*

apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter"
    compile "org.springframework.boot:spring-boot-starter-logging"

    compile "org.mnode.ical4j:ical4j:$ical4jVersion"
    compile "commons-io:commons-io:$commonsIoVersion"

    compile "com.google.api-client:google-api-client:$googleApiVersion"
    compile "com.google.oauth-client:google-oauth-client-jetty:$googleApiVersion"
    compile "com.google.apis:google-api-services-calendar:$googleCanedarApiVersion"

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    compileOnly "com.google.code.findbugs:jsr305:$jsr305Version"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile "junit:junit:$junitVersion"
}

jar {
    archiveBaseName = "ical-to-google-calendar"
    version = "0.0.1-SNAPSHOT"
    archiveFileName = "${archiveBaseName}.${archiveExtension}"
}

docker {
    registryCredentials {
        username = project.properties['docker.hub.username']
        password = project.properties['docker.hub.password']
    }
}

task dockerSyncJar(type: Sync) {
    from bootJar
    into "${buildDir}/docker-build"
    rename(".*\\.jar", "app.jar")
}

task dockerCopyDockerFile(type: Copy) {
    from "${projectDir}/src/main/docker"
    into "${buildDir}/docker-build"
    dependsOn dockerSyncJar
}

task dockerBuildImage(type: DockerBuildImage) {
    inputDir = file("${buildDir}/docker-build")
    tags.add("orangebuffalo/ical-to-google-calendar:latest")
    dependsOn dockerCopyDockerFile
}

task dockerPushImage(type: DockerPushImage) {
    imageName = 'orangebuffalo/ical-to-google-calendar'
    tag = 'latest'
    dependsOn dockerBuildImage
}

compileJava.dependsOn(processResources)